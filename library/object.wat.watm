
(module
	
	(import "./utility.watm")
	(import "./memory.watm")
	(import "./resource.watm")
	(import "./string.watm")
	(import "./number.watm")
	(import "./boolean.watm")
	(import "./map.watm")
	(import "./list.watm")
	(import "./function.watm")
	(import "./vector.watm")
	(import "host" "table" (table 1 anyfunc))
	(memory (import "host" "memory") 1)
	(type $void_to_void (func))
	(type $void_to_i32 (func (result i32)))
	(type $i32_to_void (func (param $object i32)))
	(type $i32_to_i32 (func (param $1 i32) (result i32)))
	(type $i32_i32_to_void (func (param $1 i32) (param $2 i32)))
	(type $i32_i32_to_i32 (func (param $1 i32) (param $2 i32) (result i32)))
	(type $i32_i32_i32_to_void (func (param $1 i32) (param $2 i32) (param $3 i32)))
	(type $i32_i32_i32_to_i32 (func (param $1 i32) (param $2 i32) (param $3 i32) (result i32)))
	(type $i32_i32_i32_i32_to_void (func (param $1 i32) (param $2 i32) (param $3 i32) (param $4 i32)))
	(type $i32_i32_i32_i32_to_i32 (func (param $1 i32) (param $2 i32) (param $3 i32) (param $4 i32) (result i32)))
	
	(func $object_bytes (result i32)
		(i32.const 12)
	)
   
	(func $object_object_prototype (result i32)
		
		(local $map i32)
		(set_local $map (call $map_new))
		(call $map_set $map (string "subject") (function "object_subject"))
		(get_local $map)
	)
	
	(func $object_new (result i32)
		
		(local $object i32)
		(set_local $object (call $memory_allocate (typeof "object") (call $object_bytes)))
		(call $object_subject_set (get_local $object) (get_local $object))
		(call $object_prototype_set (get_local $object) (i32.const 0))
		(call $object_map_set (get_local $object) (call $map_new))
		(get_local $object)
	)
	
	(func $object_clone (param $object_ i32) (result i32)
		
		(local $object i32)
		(set_local $object (call $object_new))
		(call $object_subject_set (get_local $object) (call $object_subject (get_local $object_)))
		(call $object_prototype_set (get_local $object) (call $object_prototype (get_local $object_)))
		(call $object_map_set (get_local $object) (call $object_map (get_local $object_)))
		(get_local $object)
	)
	
	(func $object_clone_with_subject (param $object_ i32) (param $subject i32) (result i32)
		
		(local $object i32)
		(set_local $object (call $object_new))
		(call $object_subject_set (get_local $object) (get_local $subject))
		(call $object_prototype_set (get_local $object) (call $object_prototype (get_local $object_)))
		(call $object_map_set (get_local $object) (call $object_map (get_local $object_)))
		(get_local $object)
	)
	
	(func $object_subject (param $object i32) (result i32)
		(call $memory_load (get_local $object) (i32.const 0))
	)
	
	(func $object_subject_set (param $object i32) (param $subject i32)
		(call $memory_store (get_local $object) (i32.const 0) (get_local $subject))
	)
	
	(func $object_prototype (param $object i32) (result i32)
		(call $memory_load (get_local $object) (i32.const 4))
	)
	
	(func $object_prototype_set (param $object i32) (param $proto i32)
		(call $memory_store (get_local $object) (i32.const 4) (get_local $proto))
	)
	
	(func $object_map (param $object i32) (result i32)
		(call $memory_load (get_local $object) (i32.const 8))
	)
	
	(func $object_map_set (param $object i32) (param $value i32)
		(call $memory_store (get_local $object) (i32.const 8) (get_local $value))
	)
	
	(func $object_set (param $object i32) (param $key i32) (param $value i32)
		(call $map_set (call $object_map (get_local $object)) (get_local $key) (get_local $value))
	)
	
	(func $object_load (param $object i32) (param $key i32) (result i32)
		
		(local $result i32)
		(set_local $result (call $map_get (call $object_map (get_local $object)) (get_local $key)))
		(if (i32.eq (get_local $result) (i32.const 0)) (then
			(set_local $result (call $map_get (call $object_prototype (get_local $object)) (get_local $key)))
		))
		(get_local $result)
	)
	
	(func $object_get (param $object i32) (param $key i32) (result i32)
		
		(local $result i32)
		(local $value i32)
		(set_local $result (call $object_load (get_local $object) (get_local $key)))
		(if (i32.eq (call $memory_type (get_local $result)) (typeof "function")) (then
			(set_local $result (call $object_call_void (get_local $object) (get_local $key)))
		))
		(get_local $result)
	)
	
	(func $object_call_void (param $object i32) (param $key i32) (result i32)
		
		(local $function i32)
		(set_local $function (call $object_load (get_local $object) (get_local $key)))
		(call $function_call_i32 (get_local $function) (get_local $object))
	)
	
	(func $object_call_i32 (param $object i32) (param $key i32) (param $a i32) (result i32)
		
		(local $function i32)
		(set_local $function (call $object_load (get_local $object) (get_local $key)))
		(call $function_call_i32_i32 (get_local $function) (get_local $object) (get_local $a))
	)
	
	(func $object_call_i32_i32 (param $object i32) (param $key i32) (param $a i32) (param $b i32) (result i32)
		
		(local $function i32)
		(set_local $function (call $object_load (get_local $object) (get_local $key)))
		(call $function_call_i32_i32_i32 (get_local $function) (get_local $object) (get_local $a) (get_local $b))
	)
	
	(func $object_call_i32_i32_i32 (param $object i32) (param $key i32) (param $a i32) (param $b i32) (param $c i32) (result i32)
		
		(local $function i32)
		(set_local $function (call $object_load (get_local $object) (get_local $key)))
		(call $function_call_i32_i32_i32_i32 (get_local $function) (get_local $object) (get_local $a) (get_local $b) (get_local $c))
	)
	
	(func $object_message_void (param $object i32) (param $key i32) (result i32)
		
		(set_local $key (call $object_subject (get_local $key)))
		(call $object_get (get_local $object) (get_local $key))
	)
	
	(func $object_message_i32 (param $object i32) (param $key i32) (param $a i32) (result i32)
		
		(set_local $key (call $object_subject (get_local $key)))
		(call $object_call_i32 (get_local $object) (get_local $key) (get_local $a))
	)
	
	(func $object_message_i32_i32 (param $object i32) (param $key i32) (param $a i32) (param $b i32) (result i32)
		
		(local $result i32)
		(set_local $result (i32.const 0))
		(set_local $key (call $object_subject (get_local $key)))
		(if (call $object_message_is_set (get_local $a)) (then
			(call $object_set (get_local $object) (get_local $key) (get_local $b))
		) (else
			(set_local $result (call $object_call_i32_i32 (get_local $object) (get_local $key) (get_local $a) (get_local $b)))
		))
		(get_local $result)
	)
	
	(func $object_message_i32_i32_i32 (param $object i32) (param $key i32) (param $a i32) (param $b i32) (param $c i32) (result i32)
		
		(set_local $key (call $object_subject (get_local $key)))
		(call $object_call_i32_i32_i32 (get_local $object) (get_local $key) (get_local $a) (get_local $b) (get_local $c))
	)
	
	(func $object_message_is_set (param $argument i32) (result i32)
		
		(local $result i32)
		(set_local $argument (call $object_subject (get_local $argument)))
		(set_local $result (i32.const 0))
		(if (i32.eq (call $memory_type (get_local $argument)) (typeof "string")) (then
			(if (i32.eq (call $string_equals (get_local $argument) (string "=")) (i32.const 1)) (then
				(set_local $result (i32.const 1))
			) (else
				(if (i32.eq (call $string_equals (get_local $argument) (string "is")) (i32.const 1)) (then
					(set_local $result (i32.const 1))
				))
			))
		))
		(get_local $result)
	)
	
	(func $object_to_string (param $object i32) (result i32)
		
		(local $result i32)
		(local $map i32)
		(local $length i32)
		(local $context i32)
		(set_local $result (string "object ("))
		(set_local $map (call $object_map (get_local $object)))
		(set_local length (call $map_length (get_local $map)))
		(set_local $context (call $map_new))
		(call $map_set (get_local $context) (string "string") (get_local $result))
		(call $map_set (get_local $context) (string "length") (get_local $length))
		(call $map_iterate (get_local $map) (function "object_to_string_iterate_each") (get_local $context))
		(set_local $result (call $map_get (get_local $context) (string "string")))
		(set_local $result (call $string_append (get_local $result) (string ")")))
		(get_local $result)
	)
	
	(func $object_to_string_iterate_each (param $map i32) (param $key i32) (param $value i32) (param $index i32) (param $context i32)
		
		(local $length i32)
		(local $string i32)
		(set_local $length (call $map_get (get_local $context) (string "length")))
		(set_local $string (call $map_get (get_local $context) (string "string")))
		(set_local $string (call $string_append (get_local $string) (get_local $key)))
		(set_local $string (call $string_append (get_local $string) (string ": ")))
		(set_local $string (call $string_append (get_local $string) (call $object_property_value_to_string (get_local $value))))
		(if (i32.lt_u (call $number_value (get_local $index)) (i32.sub (call $number_value (get_local $length)) (i32.const 1))) (then
			(set_local $string (call $string_append (get_local $string) (string ", ")))
		))
		(call $map_set (get_local $context) (string "string") (get_local $string))
	)
	
	(func $object_property_value_to_string (param $value i32) (result i32)
		
		(local $result i32)
		(local $type i32)
		(set_local $result (string "unknown"))
		(set_local $type (call $memory_type (get_local $value)))
		(if (i32.eq (get_local $type) (typeof "string")) (then
			(set_local $result (get_local $value))
		))
		(if (i32.eq (get_local $type) (typeof "number")) (then
			(set_local $result (call $number_to_string (get_local $value)))
		))
		(if (i32.eq (get_local $type) (typeof "object")) (then
			(set_local $result (call $object_to_string (get_local $value)))
		))
		(if (i32.eq (get_local $type) (typeof "function")) (then
			(set_local $result (call $function_to_string (get_local $value)))
		))
		(get_local $result)
	)
)
