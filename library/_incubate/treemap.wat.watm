(module
	
	;; perhaps deprecate: tree already maps a key to a value

	(import "./utility.watm")
	(import "./memory.watm")
	(import "./list.watm")	
	(import "./number.watm")	
	(import "host" "table" (table 1 anyfunc))
	(memory (import "host" "memory") 1)
	(type $i32_i32_i32_i32_to_void (func (param i32) (param i32) (param i32) (param i32)))
	
	(func $treemap_new (result i32)
		
		(local $map i32)
		(set_local $map (call $memory_allocate (typeof "treemap") (i32.const 12)))
		(i32.store (i32.add (get_local $map) (i32.const 0)) (i32.const 12))			;; type
		(i32.store (i32.add (get_local $map) (i32.const 4)) (call $list_new))		;; keys
		(i32.store (i32.add (get_local $map) (i32.const 8)) (call $list_new))		;; values
		(get_local $map)
	)
	
	(func $treemap_keys (param $map i32) (result i32)
		(i32.load (i32.add (get_local $map) (i32.const 4)))
	)
	
	(func $treemap_values (param $map i32) (result i32)
		(i32.load (i32.add (get_local $map) (i32.const 8)))
	)

	(func $treemap_set (param $map i32) (param $key i32) (param $value i32)
		
		(local $index i32)
		(local $keys i32)
		(local $values i32)
		(set_local $keys (call $treemap_keys (get_local $map)))
		(set_local $values (call $treemap_values (get_local $map)))
		(set_local $index (call $list_index_of (get_local $keys) (get_local $key)))
		(if
			(i32.eq (get_local $index) (i32.const -1))
			(then 
				(call $list_append (get_local $keys) (get_local $key))
				(call $list_append (get_local $values) (get_local $value))
			)
			(else (call $list_set (get_local $values) (get_local $index) (get_local $value)))
		)
	)
	
	(func $treemap_get (param $map i32) (param $key i32) (result i32)
		
		(local $result i32)
		(local $index i32)
		(local $keys i32)
		(local $values i32)
		(set_local $keys (call $treemap_keys (get_local $map)))
		(set_local $values (call $treemap_values (get_local $map)))
		(set_local $index (call $list_index_of (get_local $keys) (get_local $key)))
		(if
			(i32.eq (get_local $index) (i32.const -1))
			(then (set_local $result (i32.const -1)))
			(else (set_local $result (call $list_get (get_local $values) (get_local $index))))
		)
		(get_local $result)
	)

	(func $treemap_iterate (param $map i32) (param $function i32) (param $context i32)
		
		(local $index i32)
		(local $keys i32)
		(local $values i32)
		(local $key i32)
		(local $value i32)
		(set_local $index (i32.const 0))
		(set_local $keys (call $treemap_keys (get_local $map)))
		(set_local $values (call $treemap_values (get_local $map)))
		(set_local $key (get_local $keys))
		(block (loop
			(set_local $key (call $item_next (get_local $key)))
			(set_local $value (call $list_get (get_local $values) (get_local $index)))
			(br_if 1 (i32.eq (get_local $key) (get_local $keys)))
			(call_indirect (type $i32_i32_i32_i32_to_void)
				(call $item_value (get_local $key)) (get_local $value) (get_local $index) (get_local $context)
				(get_local $function)
			)
			(set_local $index (i32.add (get_local $index) (i32.const 1)))
			(br 0)
		))
	)
)
